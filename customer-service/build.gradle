plugins {
    id 'java'
    id 'application'
    id 'org.liquibase.gradle' version '2.0.3'
}

group 'com.wks.servicemarketplace'
version '1.0-SNAPSHOT'

sourceCompatibility = 13

repositories {
    mavenCentral()
    jcenter()
}


jar {
    manifest {
        attributes "Main-Class": "com.wks.servicemarketplace.customerservice.CustomerServiceApplication"
    }
    zip64 = true
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    implementation 'com.wks.servicemarketplace:common:1.0-SNAPSHOT'
    implementation 'com.wks.servicemarketplace:customer-services-api:1.0-SNAPSHOT'
    implementation 'com.wks.servicemarketplace:auth-service-messages:1.0-SNAPSHOT'
    implementation 'com.wks.servicemarketplace:auth-service-api:1.0-SNAPSHOT'
    implementation 'com.wks.servicemarketplace:customer-service-messaging:1.0-SNAPSHOT'

    // Jersey
    implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: '2.31'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.31'

    // Jackson
    implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.4.1' // Jackson Provider for Jax-RS
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.0' // Support for java 8's time API
    implementation 'javax.activation:activation:1.1.1' // Needed by jackson-jaxrs-json-provider

    // Logging
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'org.slf4j:slf4j-log4j12:1.7.30'

    // Liquibase
    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
    liquibaseRuntime group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    liquibaseRuntime("ch.qos.logback:logback-core:1.2.3")
    liquibaseRuntime("ch.qos.logback:logback-classic:1.2.3")
    liquibaseRuntime("org.postgresql:postgresql:42.2.12")

    // PostgreSQL
    implementation 'org.postgresql:postgresql:42.2.12'

    // Hikari
    implementation 'com.zaxxer:HikariCP:3.4.5'

    // JOOQ
    implementation 'org.jooq:jooq:3.12.3'
    implementation 'org.jooq:jooq-meta:3.12.3'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    // Validation
    implementation 'org.glassfish:javax.el:3.0.0'
    implementation 'javax.el:javax.el-api:3.0.0'
    implementation 'org.hibernate:hibernate-validator:6.1.5.Final'

    // Quartz
    implementation "org.quartz-scheduler:quartz:2.3.0"

    // Utils
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'

    // JWK
    implementation "org.bitbucket.b_c:jose4j:0.7.2"

    // Metrics
    implementation "io.dropwizard.metrics:metrics-core:4.1.2"
    implementation "io.dropwizard.metrics:metrics-healthchecks:4.1.2"
    implementation "io.dropwizard.metrics:metrics-servlets:4.1.2"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

liquibase {
    // Usage e.g. gradle liquibaseUpdate -Purl=jdbc:postgresql://localhost:5432/account -Pusername= -Ppassword= -PrunList=main
    activities {
        main {
            changeLogFile 'src/main/resources/liquibase/customerService.changelog.xml'
            url project.ext.mainUrl
            username project.ext.username
            password project.ext.password
        }
        runList project.ext.runList
    }
}

